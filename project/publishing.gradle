apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def scmUrl = 'https://github.com/rainmore/scorm-imsmanifest-java'

// Create the pom configuration:
def pomConfig = {
    name rootProject.name
    description project.description
    inceptionYear '2019'
    packaging 'jar'

    licenses {
        license {
            name 'Apache License 2.0'
            url 'https://github.com/rainmore/scorm-imsmanifest-java/blob/master/LICENSE'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'rainmore'
            name 'Jie Rong'
            email 'rainmore24@gmail.com'
        }
    }

    scm {
        url scmUrl
    }
}

// Create the publication with the pom configuration:
publishing {
    publications {
        main(MavenPublication) {
            from components.java

            artifact source: sourceJar,  classifier: 'sources'
            artifact source: javadocJar, classifier: 'javadoc'

            pom.withXml {
                asNode().children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.ext.get('jfrog.bintray.username')
    key  = project.ext.get('jfrog.bintray.apiKey')
    configurations = ['archives']
    pkg {
        repo = 'scorm-imsmanifest-java'
        name = 'au.com.rainmore.scorm.manifesttypes'
        licenses = ['Apache License 2.0']
        vcsUrl = scmUrl
    }
}
